#! /usr/bin/gnuplot -persist

#set title 'PE scan along N-H bond (N-side) - SS-CASPT2//SA-CASSCF/aug-cc-pVDZ'

set style line  1 lt 1 lc rgbcolor "#001999" lw 6 pt 13 ps 1 # darkblue
set style line  2 lt 1 lc rgbcolor "#ba2321" lw 6 pt 1  ps 1.5 # dark red
set style line  3 lt 1 lc rgbcolor "#117314" lw 6 pt 7  ps 1 # dark green
set style line  4 lt 1 lc rgbcolor "#333333" lw 6 pt 4  ps 1 # dark grey

set xrange [0:90]
set yrange [0:11]
set ylabel '\large Energy [eV]' offset 2,0
set xlabel '\large rotation angle'
set mxtics 2
set mytics 2
set grid xtics mxtics ytics mytics
set key top right vertical Left reverse nobox width 2 height +0 samplen 2 spacing 1.5
set terminal epslatex color standalone size 12cm, 10cm  blacktext "" 11 \
    header "\\usepackage[T1]{fontenc}\n\\renewcommand{\\familydefault}{\\sfdefault}"
set output 'plot.tex'
set label at first  0,2  '\includegraphics[scale=0.1,angle=0]{frame_0001/montage.0.PNG}'
set label at first 30,6  '\includegraphics[scale=0.1,angle=0]{frame_0005/montage.0.PNG}'
set label at first 50,2  '\includegraphics[scale=0.1,angle=0]{frame_0010/montage.0.PNG}'
Min = -77.99449359
HeV = 27.211
plot\
	'output.dat' using ($1):($3-Min)*HeV every :::0::0 smooth csplines w l ls 2 t '$\pi^{2}$',\
	'output.dat' using ($1):($3-Min)*HeV every :::1::1 smooth csplines w l ls 3 t '$\pi\pi^{\ast}$'
